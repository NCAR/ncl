#!/bin/csh -f
#
#      $Id: Configure,v 1.47 2009-05-01 03:45:45 haley Exp $
#
#########################################################################
#																		#
#			   Copyright (C)  1994										#
#	     University Corporation for Atmospheric Research				#
#			   All Rights Reserved										#
#########################################################################
#
#	File:		Configure		
#
#	Author:		Jeff W. Boote
#				National Center for Atmospheric Research
#				POB 3000, Boulder, Colorado
#
#	$Date: 2009-05-01 03:45:45 $
#
#	Description:	I took pieces of the old configure script to
#					build this one.

onintr cleanup

set cmd = $0
set cmddir = ${cmd:h}
set cmdname = ${cmd:t}
if (($cmddir != $cmdname) && ($cmddir != ".")) then
	echo "Must be at top of NCARG source tree."
	echo "cd to $cmddir and try again."
	goto cleanup
endif

if ((! -d config) || (! -x config/ymkmf)) then
	echo "Where is the config/ymkmf program???"
	echo "It should be at ./config/ymkmf"
	goto cleanup
endif

set ymkmfcmd=./config/ymkmf

if ($#argv > 0) then
	while ($#argv)
		switch($argv[1])
		case "-debug":
			set verbose
			breaksw
		case "-v":
			set verbose_option
			breaksw
		case "-ncar":
			set ncar_option
			breaksw
		default:
			echo "Error: Unknown command-line arguement."
			exit 1
			breaksw
		endsw
		shift
	end
endif

foreach dir ($path)
	if (-x $dir/make) then
		set makecmd=$dir/make
		break
	endif
end

if (! $?makecmd) then
	echo "This Configure Script depends upon the 'make' program."
	echo "It doesn't appear to be available.  Please check your PATH"
	echo "and restart this script."
	goto cleanup
endif

foreach dir ($path)
	if (-x $dir/makedepend) then
		set makedependcmd=$dir/makedepend
		break
	endif
end

if(! $?makedependcmd) set nomakedepend

if (-e ./config/Site.local) then
	echo "	*** WARNING:There is a previous configuration saved."
	echo ""
	echo "Overwrite existing configuration? (n)"
	echo -n "Enter Return(default), y(yes), n(no), or q(quit) > "
	set answer="$<"
	if ("$answer" == "q") goto cleanup
	if ("$answer" == "y") then
		if ($?ncar_option) then
			rm ./config/Site.local
		endif
		goto overwrite
	endif
	goto shortcut
endif

overwrite:

if ($?ncar_option) goto shortcut

onintr extra_cleanup

echo "/* Default Config */" > config/Site.local
echo "#define EmptySite" >> config/Site.local
if ($?nomakedepend) echo "#define NoMakeDepend" >> config/Site.local

echo ""
echo "Building top-level Makefile to determine System defaults"
echo ""
$ymkmfcmd
if ($status) then
	echo "Unable to build Makefile - fix above errors and re-run."
	goto cleanup
endif
echo ""
echo "Makefile built, checking defaults..."
echo ""
echo ""

set parentdir=`$makecmd ROOT`
echo -n "..."
set tmpdir=`$makecmd TROOT`
echo -n "..."
set loclib=`$makecmd LSEARCH`
echo -n "..."
set locinc=`$makecmd ISEARCH`
echo -n "..."
#
# The python stuff is commented out for now. One should use
# the "setup.py" method for building python software, and not
# the ymake system.
#
set buildncl=`$makecmd NCLSTUFF`
echo -n "..."
# set pythondir=`$makecmd PYTHONDIR`
# echo -n "..."
# set pythonversion=`$makecmd PYTHONVERS`
# echo -n "..."
set buildxlibstuff=`$makecmd XSTUFF`
echo -n "..."
set buildtriangle=`$makecmd TRIANGLESTUFF`
echo -n "..."
set buildpng=`$makecmd PNGSTUFF`
echo -n "..."
set buildcairo=`$makecmd CAIROSTUFF`
echo -n "..."
set buildgdal=`$makecmd GDALSTUFF`
echo -n "..."
set buildhdf=`$makecmd HDFSTUFF`
echo -n "..."
set buildnetcdf4=`$makecmd NETCDF4STUFF`
echo -n "..."
set buildudunits=`$makecmd UDUNITSSTUFF`
echo -n "..."
set buildhdfeos=`$makecmd HDFEOSSTUFF`
echo -n "..."
set buildgrib2=`$makecmd GRIB2STUFF`
echo -n "..."
set buildv5d=`$makecmd V5DSTUFF`
echo -n "..."
# set buildpyngl=`$makecmd PYNGLSTUFF`
# echo -n "..."
set builddods=`$makecmd DODSSTUFF`
echo -n "..."
set ncargversion=`$makecmd NGVERS`
echo -n "..."
set nclversion=`$makecmd NCLVERS`
echo ""
/bin/rm Makefile

onintr cleanup

QuestionAndAnswer:
echo ""
echo "*** Configuration Procedure for NCL and NCAR Graphics V$nclversion ***"
echo ""

if ($?verbose_option) then
  cat <<"EOF"

	This question and answer session will allow you to configure
	NCAR Graphics for installation.  It creates a file "Site.local"
	in the "config" directory that defines Site specific information.
	This procedure will also check that the
	directories specified are accessible and that the required
	system support is available.

"EOF"

  echo -n "Enter Return to continue, or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup

  cat <<"EOF"

	Informational text (like the paragraph you are reading) is
	indented by one tab stop.  Questions are left justified;
	usually, the default answer is in parentheses at the end of
	the question.  At the prompt, you may select the default by
	entering a Return (carriage return) or you may enter any of
	the other valid responses.

"EOF"

  echo -n "Enter Return to continue, or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup

endif

echo -n "Enter Return to continue, or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup

#
# Whether to build NCL or not.
#
if (-e ni) then
  if ($?verbose_option) then
    cat <<"EOF"

	The NCAR Command Language (NCL) is a free interpreted language
        designed specifically for scientific data processing and
        visualization. NCL has robust file input and output. It can
        read in netCDF, HDF4, HDF4-EOS, GRIB, binary and ASCII data.
        It requires several external packages (like netCDF and HDF),
        and has several optional packages you can build in (UDUNITS,
        Triangle, HDF-EOS, and Vis5D functions).

        NCL is a separate software package from NCAR Graphics. You
        don't don't need it in order to use NCAR Graphics.
"EOF"
  endif

  echo ""
  echo "Build NCL ($buildncl)?"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildncl")) then
    set buildncl = $answer
    set newbuildncl
    set altered
  endif
else
#  echo ""
#  echo "Not building NCL...source code directory is not available."
  set buildncl = "n"
  set buildudunits = "n"
  set buildtriangle = "n"
  set newbuildncl
  set altered
endif

#
# Parent installation directory for NCL/NCAR Graphics.
#

proc_parent:
if ($?verbose_option) then
  cat <<"EOF"

	The NCL/NCAR Graphics software must be installed in a parent 
        directory containing subdirectiries 'bin', 'include', 'lib',
        and 'man'. This procedure will make sure that these
        directories are writable by the installation process.

"EOF"
endif

echo ""
echo "Parent installation directory : $parentdir"
echo -n "Enter Return (default), new directory, or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if ("$answer" != "") then
  set parentdir="$answer"
  set altered
endif

if (-d $parentdir) then
  if (! -w "$parentdir") then
    echo "<$parentdir> is not writable."
    echo "New directory name? (y)"
    echo -n "Enter Return(default), y(yes), n(no), or q(quit) > "
    set answer="$<"
    if ("$answer" == "q") goto cleanup
    if ("$answer" == "y" || "$answer" == "") goto proc_parent
  endif
endif

#
# Make sure subdirs bin, include, lib, and man are writable.
#

set instdirs = ("bin" "include" "lib" "man")

foreach d($instdirs)
  set dir = "$parentdir/$d"
  if (-d "$dir") then
    if (! -w "$dir") then
      echo "<$dir> is not writable"
      echo "New parent directory name? (y)"
      echo -n "Enter Return(default), y(yes), n(no), or q(quit) > "
      set answer="$<"
      if ("$answer" == "q") goto cleanup
      if ("$answer" == "y" || "$answer" == "") goto proc_parent
    endif
  endif
end

#
# Temp space directory.
#

proc_tmp:

if ($?verbose_option) then
  cat <<"EOF"

	A couple of the translators make use of temporary
	file space.  On most systems, the directory "/tmp" is the
	logical choice.  You may specify a different directory to
	use for this purpose.  The directory must be writable by
	NCAR Grahpics users.  This procedure will check only to
	make sure that the directory specified is writable by the
	installation process.

"EOF"
endif

echo ""
echo "System temp space directory   : $tmpdir"
echo -n "Enter Return (default), new directory, or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if ("$answer" != "") then
  set tmpdir="$answer"
  set newtmp
  set altered
endif

if (-d "$tmpdir") then
  if (! -w "$tmpdir") then
    echo "<$tmpdir> is not writable"
    echo "New directory name? (y)"
    echo -n "Enter Return(default), y(yes), n(no), or q(quit) > "
    set answer="$<"
    if ("$answer" == "q") goto cleanup
    if ("$answer" == "y" || "$answer" == "") goto proc_tmp
  endif
endif

#
# NCAR View stuff.
#

if ($?verbose_option) then
  cat <<"EOF"

	The next few questions concern some of the applications in
	NCAR Graphics that allow the user to manipulate and view
	NCAR Graphics Computer Graphics	Metafiles (NCGM's).  For
	example, "ctrans" is an NCGM translator that can support the
	X Window System, PostScript, and many other graphics
	devices, and "idt" is an X window interactive image display
	tool which allows the user to do animation among other
	things.  There are also several raster applications which
	allow you to convert from one raster format to another,
	split raster files, and view raster files.

	In order to build pieces of these applications, you need the
	basic X11 libraries.  If you don't have these libraries,
	answer "n" to the next question.

"EOF"
endif

#
# PNG
#
if ($?verbose_option) then
  cat <<"EOF"

	There's some experimental code in the GKS library in NCAR
	Graphics that allows you to create PNG files in the
	same way that you can output to an NCGM, PS, or PDF file. 
	If you want to try this code, then you'll need to download
	and install the png and zlib source code and link against
	their libraries whenever you link an NCAR Graphics program.

"EOF"
endif

echo ""
echo "Build png/zlib support (optional) into GKS library? ($buildpng)"
echo "(requires the external png/zlib libraries available from"
echo "http://www.libpng.org/pub/png/libpng.html and http://www.zlib.net/) "
echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if (("$answer" != "") && ("$answer" != "$buildpng")) then
	set buildpng = $answer
	set newbuildpng
    set altered
endif

#
# Cairo
#
if ($?verbose_option) then
  cat <<"EOF"

	If you want access to the cairo workstations, like PNG,
	then you'll need to download several packages along with
        cairo. This is experimental for now, so unless you know
        what you're doing, answer "n" to this question.

"EOF"
endif

echo ""
echo "Build cairo support (optional) into GKS library? (we recommend 'n') ($buildcairo)"
echo "(requires the cairo and other external libraries available from"
echo "http://cairographics.org/) "
echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if (("$answer" != "") && ("$answer" != "$buildcairo")) then
	set buildcairo = $answer
	set newbuildcairo
    set altered
endif

#
# HDF. This is not optional if you are building NCL.
#
if ($?verbose_option) then
  cat <<"EOF"

	If you answered yes to the "build NCL?" question, then you must
	build the HDF libraries according to the NCL/NCAR Graphics
	source code installation instructions at:

	http://www.ncl.ucar.edu/Download/build_from_src.shtml#HDF-4

	If you try to build HDF without reading these instructions,
	you will run into problems with trying to link against the
	HDF libraries and include files.

	If you are not building NCL, and are only interested in building
	NCAR Graphics, then you only need to build HDF if you need
	HDF support built into the NCAR Graphics raster library.

"EOF"
endif

if ("$buildncl" == "y" || "$buildncl" == "Y") then
  echo ""
  echo "Informational note: since you are building NCL, you must build the"
  echo "external HDF libraries available from http://hdf.ncsa.uiuc.edu/."
  echo "Please see the instructions at"
  echo "http://www.ncl.ucar.edu/Download/build_from_src.shtml#HDF-4"
  echo "to make sure your HDF software is built according to NCL requirements."
else
  echo ""
  echo "Build HDF support (optional) into raster library? ($buildhdf)"
  echo "(requires the external HDF libraries available from http://hdf.ncsa.uiuc.edu/) "
  echo "(Note: HDF is required if you plan to build NCL.)"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildhdf")) then
  	set buildhdf = $answer
  	set newbuildhdf
    set altered
  endif
endif

set hdfwithszip = "n"
if (("$buildhdf" == "y") || ("$buildhdf" == "Y")) then
    echo ""
    echo "Did you build HDF-4 with szip support? ($hdfwithszip)"
    echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
    set answer="$<"
    if ("$answer" == "q") goto cleanup
    if (("$answer" != "") && ("$answer" != "$hdfwithszip")) then
        set hdfwithszip = "$answer"
        set hdflib = "-lmfhdf -ldf -ljpeg -lz -lsz"
    endif
endif

#
# Only ask about NetCDF- UDUNITS, HDFEOS, Triangle, Vis5d+, and
# other external software support if we are actually planning to
# build NCL.
#
if ("$buildncl" == "y" || "$buildncl" == "Y") then
#
# Triangle
#
  if ($?verbose_option) then
    cat <<"EOF"

	The Triangle code is what allows you to generate triangular
        meshes so that you can contour non-uniform grids.
	In order to have this functionality, you must download the
        Triangle code from http://www.cs.cmu.edu/~quake/triangle.html
        and agree to Jonathan Shewchuk's license restrictions that
        are mentioned on that page. Once you download the code
        ('triangle.c' and 'triangle.h'), put these two files in the 
        ni/src/lib/hlu subdirectory, answer "y" to this question,
        and you should be set.

"EOF"
  endif

  echo ""
  echo "Build Triangle support (optional) into NCL ($buildtriangle)"
  echo "Requires 'triangle.c' and 'triangle.h' code from"
  echo " http://www.cs.cmu.edu/~quake/triangle.html."
  echo "You must agree to the license restrictions in the above URL,"
  echo "download these two files, and put them in ni/src/lib/hlu"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildtriangle")) then
	set buildtriangle = $answer
	set newbuildtriangle
        set altered
  endif


#
# NetCdf-4
#
  if ($?verbose_option) then
    cat <<"EOF"

	Linking with the netCDF-4 library currently provides you
        with compression support, and the ability to write netCDF-4
        classic files. Not all aspects of netCDF-4 files are fully
        supported yet, like multiple-unlimited dimensions and
        new types. THIS LIBRARY IS NOT NEEDED FOR BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build NetCDF-4 support (optional) into NCL ($buildnetcdf4)"
  echo "(requires the external NetCDF-4 library available from http://www.unidata.ucar.edu/software/netcdf-4/) "
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildnetcdf4")) then
	set buildnetcdf4 = $answer
	set newbuildnetcdf4
        set altered
  endif

#
# GDAL
#
if ($?verbose_option) then
  cat <<"EOF"

	If you want to be able to read shapefiles, then you'll need to
        download GDAL and supporting packages (PROJ4). This is
        experimental for now, so unless you know what you're doing,
        answer "n" to this question.

"EOF"
endif

echo ""
echo "Build GDAL support (optional) into NCL? (we recommend 'n') ($buildgdal)"
echo "(requires GDAL and PROJ4 from http://www.gdal.org/ and "
echo "http://trac.osgeo.org/proj/) "
echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if (("$answer" != "") && ("$answer" != "$buildgdal")) then
	set buildgdal = $answer
	set newbuildgdal
    set altered
endif

#
# Udunits
#
  if ($?verbose_option) then
    cat <<"EOF"

	The udunits library supports many different time formats,
	and some of its functionality is available as built-in
	functions in NCL. In order to have this functionality, you must
        have the libudunits.a library installed on your system.  THIS
        LIBRARY IS NOT NEEDED FOR BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build Udunits support (optional) into NCL ($buildudunits)"
  echo "(requires the external Udunits library available from http://www.unidata.ucar.edu/packages/udunits/) "
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildudunits")) then
	set buildudunits = $answer
	set newbuildudunits
        set altered
  endif

#
# Vis5d+
#
  if ($?verbose_option) then
    cat <<"EOF"

	The Vis5d+ library supports OpenGL-based volumetric visualization
	for scientific datasets in 3+ dimensions.  Enabling this option will
	allow for writing Vis5d+ files.  It DOES NOT enable NCL or NCAR GRAPHICS
	to visualize such files.  In order to have this functionality, you must
	have the NCAR-supplied libraries installed on your system.
	THIS LIBRARY IS NOT NEEDED FOR BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build Vis5d+ support (optional) into NCL ($buildv5d)"
  echo "(requires the external Vis5d+ software available from http://vis5d.sourceforge.net/)"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildv5d")) then
	set buildv5d = $answer
	set newbuildv5d
        set altered
  endif

#
# HDFEOS
#
  if ($?verbose_option) then
    cat <<"EOF"

	The hdfeos library supports the HDF-EOS data format, and NCL
	has some capability to read these files in. In order to have
	this functionality, you must have the HDFEOS libraries
	installed on your system.  THIS LIBRARY IS NOT NEEDED FOR
	BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build HDFEOS support (optional) into NCL ($buildhdfeos)"
  echo "(requires the external HDFEOS libraries available from http://hdfeos.gsfc.nasa.gov/hdfeos/index.cfm) "
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildhdfeos")) then
  	set buildhdfeos = $answer
  	set newbuildhdfeos
        set altered
  endif

#
# GRIB2
#
  if ($?verbose_option) then
    cat <<"EOF"

    GRIB2 support is currently limited to TIGGE-specific data
    files.  Support for GRIB2 data from other data centers is
    planned.  In order to have this functionality, you must have
    both the NCEP GRIB2 decoder library and the jasper JPEG2000
    library installed on your system.  THIS LIBRARY IS NOT NEEDED
    FOR BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build GRIB2 support (optional) into NCL ($buildgrib2)"
  echo "(requires the GRIB2 decoder library:  http://www.nco.ncep.noaa.gov/pmb/codes/GRIB2/"
  echo " and jasper:  http://www.ece.uvic.ca/~mdadams/jasper/)"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$buildgrib2")) then
  	set buildgrib2 = $answer
  	set newbuildgrib2
        set altered
  endif

#
# OPeNDAP enabled NCL
#
  if ($?verbose_option) then
    cat <<"EOF"

	OPeNDAP is a protocol for requesting and transporting data across the web.
	The current OPeNDAP Data Access Protocol (DAP) simplifies all aspects of
	scientific data networking, allowing simple access to remote data. Local
	data can be made accessible to remote locations regardless of local
	storage format.  In order to have this functionality, you must have
	the Unidata-supplied OPeNDAP-enabled netCDF libraries installed on your
	system, which are available from this site:
            http://www.opendap.org
	THIS LIBRARY IS NOT NEEDED FOR BUILDING NCAR GRAPHICS.

"EOF"
  endif

  echo ""
  echo "Build OPeNDAP support (optional) into NCL ($builddods)"
  echo "(requires the external netCDF software available from http://www.unidata.ucar.edu/packages/dods/home/sw.html)"
  echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
  set answer="$<"
  if ("$answer" == "q") goto cleanup
  if (("$answer" != "") && ("$answer" != "$builddods")) then
	set builddods = $answer
	set newbuilddods
        set altered
  endif

else
  set buildhdfeos  = "n"
  set buildnetcdf4 = "n"
  set buildtriangle = "n"
  set buildudunits = "n"
  set buildgrib2 = "n"
  set buildv5d = "n"
  set builddods = "n"
  set newbuildhdfeos
  set newbuildnetcdf4
  set newbuildtriangle
  set newbuildudunits
  set newbuildgrib2
  set newbuildv5d
  set newbuilddods
  set altered
endif

#
# The python stuff is commented out for now. One should use
# the "setup.py" method for building python software, and not
# the ymake system.
#
# PyNGL
#
# if (-e pyngl) then
#   if ($?verbose_option) then
#     cat <<"EOF"
# 
#     PyNGL is a Python module that allows you to access NCL's graphical
#     library.  Enabling this option will	cause PyNGL to be built, but
#     not installed.  In order to have this functionality, you must
#     have Python and Numeric installed on your system.
# 
# "EOF"
#   endif
# 
#   echo ""
#   echo "Build PyNGL ($buildpyngl)"
#   echo "(requires Python from python.org and Numeric from www.pfdubois.com/numpy/"
#   echo -n "Enter Return (default), y(yes), n(no), or q(quit) > "
#   set answer="$<"
#   if ("$answer" == "q") goto cleanup
#   if (("$answer" != "") && ("$answer" != "$buildpyngl")) then
# 	set buildpyngl = $answer
# 	set newbuildpyngl
#         set altered
#   endif
#   if ("$buildpyngl" == "y") then
# 
# #
# # Get Python version
# # 
#     set pythonversion = "$pythonversion"
# proc_pythonversion:
#     echo ""
#     echo "Enter Python version : $pythonversion"
#     echo -n "Enter Return (default), new version, or q(quit) > "
#     set answer="$<"
# 
#     if ("$answer" == "q") goto cleanup
#     if ("$answer" != "") then
#       set pythonversion = "$answer"
#       set npythonvers
#       set altered
#     endif
# #
# # Get Python path. This must be the path leading up to the lib, bin, 
# # and include directories associated with Python.
# # 
#     set pythondir = "$pythondir"
# proc_pythondir:
#     echo ""
#     echo "Enter Python path : $pythondir"
#     echo "(enter path leading up to Python's lib, bin, and include directories.)"
#     echo -n "Enter Return (default), new directory, or q(quit) > "
#     set answer="$<"
# 
#     if ("$answer" == "q") goto cleanup
#     if ("$answer" != "") then
#       set pythondir = "$answer"
#       set npythondir
#       set altered
#     endif
#     if ("$pythondir" != "") then
#       if (! -d $pythondir) then
#         echo "  *** Warning: <$pythondir> does not exist"
#         echo ""
#         goto proc_pythondir
#       endif
#     endif
#     set pythonincdir = "$pythondir/include/python$pythonversion"
#     set pythonlibdir  = $pythondir/lib
#     set pythonbindir  = $pythondir/bin
#     set pythonpkgs   = "$pythonlibdir/python$pythonversion/site-packages"
#     if (! -d $pythonbindir || ! -d $pythonpkgs || ! -d $pythonincdir) then
#       echo "  *** Warning: <$pythonpkgs> and/or <$pythonincdir> does not exist"
#       echo ""
#       goto proc_pythondir
#     endif
#     set pythonlib = "-L$pythonlibdir"
#     set pythoninc = "-I$pythonincdir"
# # 
# # Decide whether to use Numeric or NumPy.
# # 
#     set usenumpy = 0
#     echo ""
#     echo "Use Numeric (0) or NumPy (1)? : $usenumpy "
#     echo -n "Enter Return (default), (0) Numeric, (1) NumPy, or q(quit) > "
#     set answer="$<"
# 
#     if ("$answer" == "q") goto cleanup
#     if ("$answer" != "") then
#       set usenumpy = "$answer"
#       set altered
#     endif
#   endif 
# endif

#
#  Library Search Path
#

if ($?verbose_option) then
  cat <<"EOF"

	Please enter the list of local libraries you wish to have
	on your library search path.  If building X applications,
	include the pathname of your X11 libraries.  Enter each
	library pathname with a space between.

"EOF"
endif

set loclibdir = `echo "$loclib" | sed -e 's/-L//g'`

proc_loclibdir:
echo ""
echo "Enter local library search path(s) : $loclibdir"
echo -n "Enter Return (default), new directories, or q(quit) > "
set answer="$<"

if ("$answer" == "q") goto cleanup
if ("$answer" != "") then
  set loclibdir = "$answer"
  set nloclib
  set altered
endif
if ("$loclibdir" != "") then
  set libs = ($loclibdir:x)
  set newloclib=""
  while ($#libs > 0)
    if (! -d $libs[1]) then
      echo "  *** Warning: <$libs[1]> does not exist"
      echo ""
      goto proc_loclibdir
    else 
      set newloclib = ($newloclib "-L$libs[1]")
    endif
    shift libs
  end
  set loclib = "$newloclib"
endif

#
# X11 include files.
#

if ($?verbose_option) then
  cat <<"EOF"

	Please enter the list of local include paths you wish to
	have on your include search path.  If building X
	applications, include the pathname to your X11 include
	files.  Enter each include pathname with a space between.

"EOF"
endif



set locincdir = `echo "$locinc" | sed -e 's/-I//g'`

proc_locincdir:
echo ""
echo "Enter local include search path(s) : $locincdir"
echo -n "Enter Return (default), new directories, or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if ("$answer" != "") then
  set locincdir = "$answer"
  set nlocinc
  set altered
endif
if ("$locincdir" != "") then
  set incs = ($locincdir:x)
  set newlocinc=""
  while ($#incs > 0)
    if (! -d $incs[1]) then
      echo "  *** Warning: <$incs[1]> does not exist"
      echo ""
      goto proc_locincdir
    else 
      set newlocinc = ($newlocinc "-I$incs[1]")
    endif
    shift incs
  end
  set locinc = "$newlocinc"
endif

#
# Summarize configuration
#
cat <<EOF

	Every user of NCL and NCAR Graphics will need to set
	the following environment variable:

        setenv NCARG_ROOT $parentdir

EOF
	
endif

echo ""
echo "Go back and make more changes or review? (n) "
echo -n "Enter Return(default), y(yes), n(no), or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup
if ("$answer" == "y") goto QuestionAndAnswer

#
# Save the configuration
#

proc_save:
echo ""
echo "Save current configuration ? (y) "
echo -n "Enter Return(default), y(yes), or q(quit) > "
set answer="$<"
if ("$answer" == "q") goto cleanup

if ("$answer" != "y" && "$answer" != "") then
	echo "Invalid response"
	goto proc_save
endif

if (! $?altered) goto shortcut

cd config

echo "/*" > Site.local
echo " *	This file was created by the Configure script." >> Site.local
echo " */" >> Site.local
echo "" >> Site.local
echo "#ifdef FirstSite" >> Site.local
echo "" >> Site.local
echo "#endif /* FirstSite */" >> Site.local
echo "" >> Site.local
echo "" >> Site.local
echo "#ifdef SecondSite" >> Site.local
echo "" >> Site.local

echo "#define YmakeRoot $parentdir" >> Site.local

if ($?newtmp) then
	echo "#define TmpRoot $tmpdir" >> Site.local
endif

echo "" >> Site.local

if ("$hdfwithszip" == "y") then
    echo "#define HDFlib $hdflib" >> Site.local
    echo "" >> Site.local
endif


if ($?nloclib) then
	echo "#define LibSearch $loclib" >> Site.local
endif
if ($?nlocinc) then
	echo "#define IncSearch $locinc" >> Site.local
endif

echo "" >> Site.local

if ($?npythonvers) then
	echo "#define PythonVersion $pythonversion" >> Site.local
endif
if ($?npythonvers || $?npythondir) then
	echo "#define PythonDir $pythondir" >> Site.local
	echo "#define PythonBinDir $pythonbindir" >> Site.local
	echo "#define PythonPkgsDir $pythonpkgs" >> Site.local
	echo "#define PythonLibSearch $pythonlib" >> Site.local
	echo "#define PythonIncSearch $pythoninc" >> Site.local
endif

echo "" >> Site.local

if ($?newbuildncl) then
	if ("$buildncl" == "y") then
		echo "#define BuildNCL TRUE" >> Site.local
	else
		echo "#define BuildNCL FALSE" >> Site.local
	endif
endif

echo "" >> Site.local

if ($?newbuildhdf) then
	if ("$buildhdf" == "y") then
		echo "#define BuildRasterHDF TRUE" >> Site.local
	else
		echo "#define BuildRasterHDF FALSE" >> Site.local
		echo "#define HDFlib" >> Site.local
	endif
endif

if ($?newbuildpng) then
	if ("$buildpng" == "y") then
		echo "#define BuildPNG TRUE" >> Site.local
	else
		echo "#define BuildPNG FALSE" >> Site.local
		echo "#define PNGlib" >> Site.local
	endif
endif

if ($?newbuildcairo  && "$buildcairo" == "y") then
	echo "#define BuildCAIRO TRUE" >> Site.local
else
	echo "#define BuildCAIRO FALSE" >> Site.local
	echo "#define CAIROlib" >> Site.local
endif

if ($?newbuildnetcdf4) then
	if ("$buildnetcdf4" == "y") then
		echo "#define BuildNetCDF4 TRUE" >> Site.local
	else
		echo "#define BuildNetCDF4 FALSE" >> Site.local
		echo "#define NetCDF4lib" >> Site.local
	endif
endif

if ($?newbuildgdal && "$buildgdal" == "y") then
	echo "#define BuildGDAL TRUE" >> Site.local
else
	echo "#define BuildGDAL FALSE" >> Site.local
	echo "#define GDALlib" >> Site.local
endif

if ($?newbuildtriangle) then
	if ("$buildtriangle" == "y") then
		echo "#define BuildTRIANGLE TRUE" >> Site.local
	else
		echo "#define BuildTRIANGLE FALSE" >> Site.local
	endif
endif

if ($?newbuildudunits) then
	if ("$buildudunits" == "y") then
		echo "#define BuildUdunits TRUE" >> Site.local
	else
		echo "#define BuildUdunits FALSE" >> Site.local
		echo "#define UdUnitslib" >> Site.local
	endif
endif

if ($?newbuildhdfeos) then
	if ("$buildhdfeos" == "y") then
		echo "#define BuildHDFEOS TRUE" >> Site.local
	else
		echo "#define BuildHDFEOS FALSE" >> Site.local
		echo "#define HDFEOSlib" >> Site.local
	endif
endif

if ($?newbuildgrib2) then
	if ("$buildgrib2" == "y") then
		echo "#define BuildGRIB2 TRUE" >> Site.local
	else
		echo "#define BuildGRIB2 FALSE" >> Site.local
		echo "#define GRIB2lib" >> Site.local
	endif
endif

if ($?newbuildv5d) then
	if ("$buildv5d" == "y") then
		echo "#define BuildV5D TRUE" >> Site.local
	else
		echo "#define BuildV5D FALSE" >> Site.local
		echo "#define V5Dlib" >> Site.local
	endif
endif

if ($?newbuilddods) then
	if ("$builddods" == "y") then
		echo "#define BuildDODS TRUE" >> Site.local
	else
		echo "#define BuildDODS FALSE" >> Site.local
	endif
endif

if ($?newbuildpyngl) then
	if ("$buildpyngl" == "y") then
		echo "#define BuildPyNGL TRUE" >> Site.local
	else
		echo "#define BuildPyNGL FALSE" >> Site.local
		echo "#define PYNGLlib" >> Site.local
	endif
	if ("$usenumpy" == 1) then
		echo "#define UseNumpy TRUE" >> Site.local
	endif
endif

echo "" >> Site.local
if ($?nomakedepend) echo "#define NoMakeDepend" >> Site.local
echo "" >> Site.local
echo "#endif /* SecondSite */" >> Site.local

cd ..

shortcut:
if ($?ncar_option) then
	set conf_type = "ncar local"
else
	set conf_type = "current"
endif
echo ""
echo "Building Top-level Makefile with $conf_type configuration options"
echo ""
$ymkmfcmd
if ($status) then
	echo "Unable to build Makefile - fix above errors and re-run."
	goto cleanup
endif

#
# Installation step
#

proc_install:
cat <<"EOF"

	You have completed the configuration process. Once you exit this
	program you may initiate the installation procedure by typing:

		make Everything >& make-output &

    You can watch the output with:

		tail -f make-output

"EOF"

if ($?verbose_option) then
cat <<"EOF"
	If this is not your first installation attempt, and you simply 
	made some changes in the configuration, restart the process using:

		make All >>& make-output &
	
	This command does not erase object code that has already
	been compiled.

	Both of these commands run the installation procedure
	in the background, and you can track its progress
	by periodically examining the file "make-output" while 
	the installation proceeds.

"EOF"
endif

exit 0

extra_cleanup:
rm -f Makefile
rm -f config/Site.local

cleanup:
echo ""
echo "Terminating configuration procedure"
exit 0
